#!/bin/zsh -f

zmodload zsh/terminfo

. ./statacus.zsh

main() {
  stInit
  parse
  echo
}

typeset -a cpath

have=n

getline() {
  local pat="$1"
  if [[ "$have" != y ]] {
    read line || return 1
    sleep .2
    have=y
  }
  if [[ "$line" =~ "$pat" ]] {
    have=n
    return 0
  }
  return 1
}

unclaim() {
  have=y
}

parse() {
  suite

  if [[ "$have" == y ]] {
    echo "unclaimed line: $line"
  }
}

suite() {
  getline '^Will run' || return 1
  local name="$line"
  start SUITE $name
  while context; do
    :
  done
  stop SUITE $name
}

context() {
  getline '\[k8s\.io\]' || return 1
  local rest="$(echo $line | perl -ne 'print $1 if m{\Q[k8s.io] '"$cpath"'\E ?(.*)}')"
  if [[ -z "$rest" ]] {
    unclaim
    return 1
  }
  local name="$line"
  start CONTEXT $name
  cpath+="$rest"
  while it || context || info; do
    :
  done
  shift -p cpath
  stop CONTEXT $name
}

it() {
  getline '^\[It\]' || return 1
  local name="$line"
  start IT $name
  while info || each || step || startit; do
    :
  done
  pass || fail || exit 2
  stop IT $name
}

startit() {
  getline '\[It\]$' || return 1
  local name="$line"
  update IT 'RUNNING'
}

pass=0
fail=0

pass() {
  getline '^PASS' || return 1
  (( pass++ ))
  update IT $line
  update SUITE "$pass passed $fail failed"
}

fail() {
  getline '^FAIL' || return 1
  (( fail++ ))
  update IT $line
  update SUITE "$pass passed $fail failed"
}

each() {
  getline '(After|Before)Each' || return 1
  local name="$line"
  start EACH $name
  while info || step; do
    :
  done
  stop EACH $name
}

step() {
  getline '^STEP:' || return 1
  local name="$line"
  start STEP $name
  info
  stop STEP $name
}

info() {
  getline '^INFO' || return 1
  unclaim
  start INFO
  while infoline; do
    :
  done
  stop INFO
}

infoline() {
  getline '^INFO' || return 1
  start INFOLINE "$line"
  stop INFOLINE "$line"
}

indent=0
start() {
  local name="$1"
  shift
  if [[ -n "$*" ]] {
    stPush "$name" $(( indent++ )) "$*"
  }
}

update() {
  local name="$1"
  shift
  stUpdate "$name" "$@"
}

stop() {
  local name="$1"
  shift
  if [[ -n "$*" ]] {
    (( indent-- ))
  }
  if [[ "$name" == 'INFO' ]] {
    stPopAll 'INFOLINE'
  } elif [[ "$name" == 'EACH' ]] {
    stPopTo 'EACH'
    stPop
  } elif [[ "$name" == 'IT' ]] {
    stPopTo 'IT'
  }
}


main "$@"
